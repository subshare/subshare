package org.subshare.gui.pgp.createkey.userid

import javafx.geometry.Insets
import javafx.scene.layout.GridPane
import javafx.scene.text.Text
import javafx.scene.control.TableColumn
import javafx.scene.control.TableView
import javafx.scene.control.cell.PropertyValueFactory

import org.subshare.gui.pgp.createkey.FxPgpUserId

dynamic component UserIdPane resourcefile 'messages.properties' {
	GridPane {
		hgap : 8,
		vgap : 8,
		padding : Insets { left : 8, top : 8, right : 8, bottom : 8 },

		Text id headerText {
			text : rstring 'UserIdPane.headerText.text',
			wrappingWidth : 500,
			static columnIndex : 0,
			static rowIndex : 0,
			static margin : Insets { bottom : 16 }
		},
		
		TableView < FxPgpUserId > id userIdsTableView {
			editable : true,

			columns : [
				TableColumn < FxPgpUserId, String > id nameTableColumn {
					text : "Name",
					prefWidth : 180,
					cellValueFactory : PropertyValueFactory < FxPgpUserId, String > {
						property : "name"
					}
				},
				TableColumn < FxPgpUserId, String > id emailTableColumn {
					text : "E-mail",
					prefWidth : 220,
					cellValueFactory : PropertyValueFactory < FxPgpUserId, String > {
						property : "email"
					}
				}
			],

			static columnIndex : 0,
			static rowIndex : 1,
			static fillWidth : true,
			static hgrow : "ALWAYS",
			static margin : Insets { left : 8 }
		}
	}
}
