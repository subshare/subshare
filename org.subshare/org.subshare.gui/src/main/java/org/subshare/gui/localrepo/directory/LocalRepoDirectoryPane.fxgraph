package org.subshare.gui.localrepo.directory

import javafx.scene.layout.GridPane
import javafx.scene.control.cell.PropertyValueFactory
import javafx.scene.control.TextField
import javafx.geometry.Insets
import javafx.scene.layout.FlowPane
import javafx.scene.control.Button
import javafx.scene.image.ImageView
import javafx.scene.image.Image
import javafx.scene.control.CheckBox
import javafx.scene.control.ComboBox
import javafx.scene.control.Tooltip
import javafx.scene.control.TabPane
import javafx.scene.control.Tab
import javafx.scene.control.TableView
import javafx.scene.control.TableColumn
import javafx.scene.layout.VBox
import javafx.scene.layout.HBox
import javafx.scene.control.Label

import org.subshare.core.dto.PermissionType
import org.subshare.gui.filetree.FileTreePane
import org.subshare.gui.userlist.UserListPane
import org.subshare.gui.selectuser.SelectUserPane
import org.subshare.gui.localrepo.directory.PermissionTypeItem
import org.subshare.gui.localrepo.directory.UserListItem

dynamic component LocalRepoDirectoryPane resourcefile 'messages.properties' {
	VBox {
		FlowPane {
			Button id syncButton {
				graphic : ImageView { image : Image { url : "@sync_24x24.png" } },
				tooltip : Tooltip { text : "Synchronize this repository now." },
				onAction : controllermethod syncButtonClicked
			},
			Button id inviteButton {
				graphic : ImageView { image : Image { url : "@invite_24x24.png" } },
				tooltip : Tooltip { text : "Invite someone to share this repository at this sub-directory.&#010;&#010;You'll select another user and an invitation token is going to be created for this user." },
				onAction : controllermethod inviteButtonClicked
			},
			Button id refreshButton { // TODO replace by or at least add automatic refresh via listeners!
				graphic : ImageView { image : Image { url : "@refresh_24x24.png" } },
				tooltip : Tooltip { text : "Refresh all data shown here." },
				onAction : controllermethod refreshButtonClicked
			},
			static margin : Insets { left : 8, right : 8, top : 8, bottom : 8 }
		},

		TabPane {
			tabClosingPolicy : "UNAVAILABLE",
			Tab {
				text : "Content",
				content : GridPane {

					TextField id pathTextField {
						editable : false,
						static columnIndex : 0,
						static rowIndex : 1,
						static margin : Insets { left: 8, top: 8, right: 8, bottom : 8 },
						static hgrow : "ALWAYS"
					},

					FileTreePane id fileTreePane {
						selectionMode: "MULTIPLE",
						refreshButtonVisible : false,
		
						static columnIndex : 0,
						static rowIndex : 2,
						static margin : Insets { left: 8, right: 8, bottom : 8 },
						static hgrow : "ALWAYS",
						static vgrow : "ALWAYS"
					}
				}
			},

			Tab {
				text : "Security",
				content : GridPane {
					CheckBox id permissionsInheritedCheckBox {
						text : "Permissions are inherited.",
						tooltip : Tooltip { text : "Inherit the permissions from the parent directory. Permissions granted here are added to the inherited permissions." },
						static columnIndex : 0,
						static rowIndex : 0,
						static margin : Insets { left: 8, top : 8, right: 8, bottom : 8 },
						static hgrow : "ALWAYS",
						static columnSpan : 2
					},

					HBox {
						Label {
							text : "Filter: ",
							static margin : Insets { left : 8}
						},
						TextField id filterTextField {
							static hgrow : "ALWAYS",
							static margin : Insets {
								left : 4
							}
						},
			
						static margin : Insets {
							top : 4,
							bottom : 4
						},
						static columnIndex : 0,
						static rowIndex : 1,
						static columnSpan : 2
					},

					TableView < UserListItem > id userTableView {
						columns : [
							TableColumn < UserListItem, String > id firstNameColumn {
								text : "First name",
								prefWidth : 100,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "firstName"
								}
							},
							TableColumn < UserListItem, String > id lastNameColumn {
								text : "Last name",
								prefWidth : 100,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "lastName"
								}
							},
							TableColumn < UserListItem, String > id emailColumn {
								text : "E-mail",
								prefWidth : 200,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "email"
								}
							},
							TableColumn < UserListItem, String > id effectivePermissionStringColumn {
								text : "Effective permission",
								prefWidth : 100,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "effectivePermissionString"
								}
							},
							TableColumn < UserListItem, String > id grantedPermissionStringColumn {
								text : "Granted permission",
								prefWidth : 100,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "grantedPermissionString"
								}
							},
							TableColumn < UserListItem, String > id inheritedPermissionStringColumn {
								text : "Inherited permission",
								prefWidth : 100,
								cellValueFactory : PropertyValueFactory < UserListItem, String > {
									property : "inheritedPermissionString"
								}
							}
						],
						static fillHeight : true,
						static fillWidth : true,
						static vgrow : "ALWAYS",
						static hgrow : "ALWAYS",
						static columnIndex : 0,
						static rowIndex : 2,
						static columnSpan : 2,
						static margin : Insets { left : 8, right : 8 }
					},

					Label id permissionTypeLabel {
						text : "Permission of selected users:",
						static columnIndex : 0,
						static rowIndex : 3,
						static margin : Insets { left : 8, top : 8 }
					},
					ComboBox<PermissionTypeItem> id permissionTypeComboBox {
						static fillHeight : true,
						static fillWidth : true,
						static hgrow : "ALWAYS",
						static columnIndex : 1,
						static rowIndex : 3,
						static margin : Insets { left: 8, right : 8, top : 8 }
					},

					CheckBox id readUserIdentityPermissionCheckBox {
						text : rstring 'LocalRepoDirectoryPane.readUserIdentityPermissionCheckBox.text',
						tooltip : Tooltip { text : rstring 'LocalRepoDirectoryPane.readUserIdentityPermissionCheckBox.tooltip.text' },
						allowIndeterminate : true,
						static hgrow : "ALWAYS",
						static columnSpan : 2,
						static columnIndex : 0,
						static rowIndex : 4,
						static margin : Insets { left : 8, top : 8, right : 8, bottom : 8 }
					} 
				}
			}
		}
	}
}
