package org.subshare.gui.user

import javafx.beans.property.StringProperty

import javafx.scene.layout.GridPane
import javafx.scene.layout.FlowPane
import javafx.scene.control.Button
import javafx.scene.image.ImageView
import javafx.scene.image.Image
import javafx.scene.control.Tooltip
import javafx.scene.control.Label
import javafx.scene.control.TextField
import javafx.scene.control.TableView
import javafx.scene.control.TableColumn
import javafx.scene.control.cell.PropertyValueFactory

import org.subshare.gui.user.EmailWrapper

dynamic component UserPane {
	GridPane {
		children : [
			FlowPane {
				Button id closeButton {
					graphic : ImageView { image : Image { url : "@close-24x24.png" } },
					tooltip : Tooltip { text : "Close this user (end editing)." },
					onAction : controllermethod closeButtonClicked
				},
				static columnIndex : 0,
				static rowIndex : 0,
				static columnSpan : 2
			},

			Label {
				text : "First name:",
				static columnIndex : 0,
				static rowIndex : 1
			},
			TextField id firstNameTextField {
				static columnIndex : 1,
				static rowIndex : 1
			},

			Label {
				text : "Last name:",
				static columnIndex : 0,
				static rowIndex : 2
			},
			TextField id lastNameTextField {
				static columnIndex : 1,
				static rowIndex : 2
			},

			TableView < EmailWrapper > id emailsTableView {
				editable : true,
	
				columns : [
					TableColumn < EmailWrapper, String > id emailTableColumn {
						text : "E-Mail",
						prefWidth : 300,
						cellValueFactory : PropertyValueFactory < EmailWrapper, String > {
							property : "value"
						}
					}
				],
				
				static columnIndex : 0,
				static rowIndex : 3,
				static columnSpan : 2
			}
		]
	}
}
